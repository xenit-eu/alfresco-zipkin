apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
    publications {
        all {
            pom {
                url = 'https://github.com/xenit-eu/alfresco-zipkin'

                scm {
                    connection = 'scm:git:git@github.com:xenit-eu/alfresco-zipkin.git'
                    developerConnection = 'scm:git:git@github.com:xenit-eu/alfresco-zipkin.git'
                    url = 'https://github.com/xenit-eu/alfresco-zipkin.git'
                }

                developers {
                    developer {
                        id = "xenit"
                        name = "Xenit Solutions NV"
                    }
                }

                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0.txt'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.toString().endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = project.hasProperty('sonatype_username') ? project.publish_username : ''
                password = project.hasProperty('sonatype_password') ? project.publish_password : ''
            }
        }
    }
}

signing {
    required { !version.toString().endsWith("SNAPSHOT") }
    useInMemoryPgpKeys(findProperty("signingKeyId"), findProperty("signingKey"), findProperty("signingPassword"))
    sign publishing.publications
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}