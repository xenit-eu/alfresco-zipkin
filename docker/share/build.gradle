plugins {
    id "eu.xenit.docker-alfresco" version "5.0.7"
}

dependencies {
    baseShareWar "org.alfresco:share:5.2.f@war"
    shareAmp project(path: ':alfresco-zipkin-share', configuration: 'amp')
}

project.tasks.buildDockerImage.dependsOn(":alfresco-zipkin-share:amp")

dockerAlfresco {
    baseImage = "docker.io/xenit/alfresco-share-community:5.2.f"

    leanImage = true

    dockerBuild {
        repository = "docker.io/xenit/alfresco-zipkin-share"
        tags = [ rootProject.version ]
        automaticTags = false
        pull = true
    }
}

buildDockerImage {
    doLast {
        project(':integration-tests').dockerCompose.environment.put('SHARE_IMAGE', getImageId())
        project(':integration-tests').dockerCompose.unconfigured.environment.put('SHARE_IMAGE', getImageId())
        project(':integration-tests').dockerCompose.mariadb.environment.put('SHARE_IMAGE', getImageId())
    }
}

docker {
    if(project.hasProperty('DOCKER_USER') && project.hasProperty('DOCKER_PASSWORD')) {
        registryCredentials {
            username = project.DOCKER_USER
            password = project.DOCKER_PASSWORD
        }
    } else {
        println "using default credentials"
    }
}