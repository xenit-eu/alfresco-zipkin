plugins {
    id "eu.xenit.docker-compose" version "5.2.0" apply false
    id 'org.ajoberstar.reckon' version "0.13.0"
}

version = "2.0.0"

reckon {
    scopeFromProp()
    snapshotFromProp()
}

allprojects {
    group = 'eu.xenit.alfred.edge'

    project.ext {
        alfresco_version = '5.2.f'
        brave_version = '5.13.3'
        zipkin_reporter_version = '2.16.3'
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://artifacts.alfresco.com/nexus/content/repositories/public/'
        }
    }

    pluginManager.withPlugin('java') {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8

        java {
            withJavadocJar()
            withSourcesJar()

        }

        reckonTagCreate.dependsOn check
    }

    pluginManager.withPlugin('eu.xenit.docker-config') {
        docker {
            if (System.getenv("DOCKER_USER") != null) {
                registryCredentials {
                    username = System.getenv("DOCKER_USER")
                    password = System.getenv("DOCKER_PASSWORD")
                }
            } else {
                logger.debug "using default credentials"
            }
        }
    }
}
